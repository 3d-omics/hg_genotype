# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.

import pandas as pd
import yaml

# Import configuration files
params = yaml.load(open("config/params.yml", "r"), Loader=yaml.SafeLoader)
features = yaml.load(open("config/features.yml", "r"), Loader=yaml.SafeLoader)
samples = pd.read_table("config/samples.tsv", comment="#", dtype="str")


# Generate useful variables
SAMPLES = samples["sample_id"].drop_duplicates().values.tolist()
SAMPLE_LIBRARY = samples[["sample_id", "library_id"]].values.tolist()
SNPEFF_DBS = features["snpeff"]["genome"]
BAM_REPORTS = ["stats.tsv", "flagstats.txt", "idxstats.tsv"]
PICARD_REPORTS = BAM_REPORTS + ["metrics.tsv"]
REGIONS_BED4 = pd.read_table(
    features["reference"]["regions"],
    dtype="str",
    comment="#",
    names=["chrom", "chromStart", "chromEnd", "name"],
)
REGIONS = REGIONS_BED4.name

NON_AUTOSOMES = set(
    features["reference"]["male_chromosomes"]
    + features["reference"]["female_chromosomes"]
    + features["reference"]["mitochondria"]
)

DIPLOID_REGIONS = REGIONS_BED4.query("chrom not in @NON_AUTOSOMES").name


# Import subworkflows
include: "rules/folders.smk"
include: "rules/helpers/__main__.smk"
include: "rules/reads/__main__.smk"
include: "rules/reference/__main__.smk"
include: "rules/align/__main__.smk"
include: "rules/variants/__main__.smk"
include: "rules/annotate/__main__.smk"
include: "rules/report/__main__.smk"


rule all:
    """Run the entire pipeline"""
    input:
        rules.report__step.input,
        rules.report__sample.input,
